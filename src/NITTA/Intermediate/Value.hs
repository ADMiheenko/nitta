{-# LANGUAGE DataKinds              #-}
{-# LANGUAGE FunctionalDependencies #-}
{-# LANGUAGE KindSignatures         #-}
{-# LANGUAGE MultiParamTypeClasses  #-}
{-# LANGUAGE QuasiQuotes            #-}
{-# LANGUAGE ScopedTypeVariables    #-}
{-# OPTIONS -Wall -Wcompat -Wredundant-constraints -fno-warn-missing-signatures #-}

{-|
Module      : NITTA.Intermediate.Value
Description : Transferable over nets values.
Copyright   : (c) Aleksandr Penskoi, 2019
License     : BSD3
Maintainer  : aleksandr.penskoi@gmail.com
Stability   : experimental
-}

-- TODO: instance Bounded (IntX w)
-- TODO: instance Bounded (FX m b w)
-- TODO: instance Arbitrary (IntX w)
-- TODO: instance Arbitrary (FX m b w)

module NITTA.Intermediate.Value
  ( -- *Type classes
    FixedPointCompatible(..)
  , Val(..)
  , DefaultX(..)
  , scalingFactor
    -- *Value types
  , IntX(..)
  , FX(..)
  ) where

import           Data.Bits
import           Data.Default
import           Data.Maybe
import           Data.Proxy
import           Data.Typeable
import           GHC.TypeLits
import           Numeric
import           Text.InterpolatedString.Perl6 (qc)
import           Text.Printf
import           Text.Regex


class ( Default x ) => DefaultX u x | u -> x where
    defX :: u -> x
    defX _ = def


-- |Type class for Value types.
class ( Typeable x, Show x, Read x, FixedPointCompatible x, Default x, PrintfArg x
      ) => Val x where
    showTypeOf :: Proxy x -> String

    -- |Reading value from verilog dump generated by `$display("%d", databus);`
    fromVerilog :: String -> x

    -- |Convert value to applicable for Verilog literal
    toVerilogLit :: x -> Integer

    -- |Helper functions to work with type in verilog
    verilogHelper :: x -> String
    verilogHelper x = [qc|
task trace;
    input integer cycle;
    input integer tick;
    input [{ finiteBitSize x }-1:0] actual;
    begin
        $write("%0d:%0d\t", cycle, tick);
        $write("actual: %d\t", actual);
        $display();
    end
endtask // trace

task assert;
    input integer cycle;
    input integer tick;
    input [{ finiteBitSize x }-1:0] actual;
    input [{ finiteBitSize x }-1:0] expect;
    input [256*8-1:0] var; // string
    begin
        $write("%0d:%0d\t", cycle, tick);
        $write("actual: %d\t", actual);
        $write("expect: %d\t", expect);
        $write("var: %0s\t", var);
        if ( !( actual === expect ) ) $write("FAIL");
        $display();
    end
endtask // assert
|]


-- |Type class for values, which contain information about fractional part of value (for fixed point arithmetics).
class ( FiniteBits a ) => FixedPointCompatible a where
    scalingFactorPower :: a -> Integer
    fractionalBitSize :: a -> Int


scalingFactor x = 2 ** fromIntegral (scalingFactorPower x)


-- for Int
instance Val Int where
    showTypeOf _ = "Int"
    toVerilogLit = fromIntegral
    fromVerilog x = fromIntegral (read x :: Integer)

instance FixedPointCompatible Int where
    scalingFactorPower _ = 0
    fractionalBitSize _ = 0



-- |Integer number with specific bit width.
newtype IntX (w :: Nat) = IntX Integer
    deriving ( Show, Eq, Ord )

instance Read ( IntX w ) where
    readsPrec d r = case readsPrec d r of
        [(x, r')] -> [(IntX x, r')]
        _         -> error $ "can not read IntX from: " ++ r

instance PrintfArg ( IntX w ) where
    formatArg (IntX x) = formatInteger x

instance Default ( IntX w ) where
    def = IntX 0

instance Enum ( IntX w ) where
    toEnum = IntX . toInteger
    fromEnum (IntX x) = fromInteger x

instance Num ( IntX w ) where
    ( IntX a ) + ( IntX b ) = IntX ( a + b )
    ( IntX a ) * ( IntX b ) = IntX ( a * b )
    abs ( IntX a ) = IntX $ abs a
    signum ( IntX a ) = IntX $ signum a
    fromInteger a = IntX $ fromInteger a
    negate ( IntX a ) = IntX $ negate a

instance Real ( IntX w ) where
    toRational ( IntX x ) = toRational x

instance Integral ( IntX w ) where
    toInteger ( IntX x ) = toInteger x
    ( IntX a ) `quotRem` ( IntX b )
        = let (a', b') =  a `quotRem` b
        in ( IntX a', IntX b' )

instance Bits ( IntX w ) where
    ( IntX a ) .&. ( IntX b ) = IntX ( a .&. b )
    ( IntX a ) .|. ( IntX b ) = IntX ( a .|. b )
    ( IntX a ) `xor` ( IntX b ) = IntX ( a `xor` b )
    complement ( IntX a ) = IntX $ complement a
    shift ( IntX a ) i = IntX $ shift a i
    rotate ( IntX a ) i = IntX $ rotate a i

    bitSize ( IntX a ) = fromMaybe undefined $ bitSizeMaybe a
    bitSizeMaybe ( IntX a ) = bitSizeMaybe a
    isSigned ( IntX a ) = isSigned a
    testBit ( IntX a ) = testBit a
    bit i = IntX $ bit i
    popCount ( IntX a ) = popCount a

instance ( KnownNat w ) => FiniteBits ( IntX w ) where
    finiteBitSize _ = fromInteger $ natVal (Proxy :: Proxy w)

instance ( KnownNat w ) => Val ( IntX w ) where
    showTypeOf _ = "IntX" ++ show (natVal (Proxy :: Proxy w))
    toVerilogLit (IntX x) = fromIntegral x
    fromVerilog = IntX . read

instance ( KnownNat w ) => FixedPointCompatible (IntX w) where
    scalingFactorPower _ = 0
    fractionalBitSize _ = 0



-- |Number with fixed point. FX m b where
--   - m the number of magnitude or integer bits
--   - b the total number of bits
--
-- fxm.b: The "fx" prefix is similar to the above, but uses the word length as
-- the second item in the dotted pair. For example, fx1.16 describes a number
-- with 1 magnitude bit and 15 fractional bits in a 16 bit word.[3]
newtype FX (m :: Nat) (b :: Nat) = FX Integer
    deriving ( Eq, Ord )

instance ( KnownNat m, KnownNat b ) => Read ( FX m b ) where
    readsPrec d r
        = let
            [(x :: Double, "")] = readsPrec d r
            result = FX $ round (x * scalingFactor result)
        in [(result, "")]

instance ( KnownNat m, KnownNat b ) => PrintfArg ( FX m b ) where
    formatArg (FX x) = formatInteger x

instance ( KnownNat m, KnownNat b ) => Show ( FX m b ) where
    show t@(FX x) = showFFloat (Just 6) (fromIntegral x / scalingFactor t :: Double) ""

instance Default ( FX m b ) where
    def = FX 0

instance ( KnownNat m, KnownNat b ) => Enum ( FX m b ) where
    toEnum x
        = let res = FX $ toInteger (x * truncate (scalingFactor res :: Double))
        in res
    fromEnum t@(FX x) = truncate (fromIntegral x / scalingFactor t :: Double)

instance ( KnownNat m, KnownNat b ) => Num ( FX m b ) where
    ( FX a ) + ( FX b ) = FX ( a + b )
    t@( FX a ) * ( FX b ) = FX ( a * b ) `shiftR` (fromInteger $ scalingFactorPower t)
    abs ( FX a ) = FX $ abs a
    signum ( FX a ) = fromInteger $ signum a
    fromInteger x
        = let res = FX $ fromIntegral (x * truncate (scalingFactor res :: Double))
        in res
    negate ( FX a ) = FX $ negate a

instance ( KnownNat m, KnownNat b ) => Real ( FX m b ) where
    toRational t@( FX x ) = toRational (fromIntegral x / scalingFactor t :: Double)

instance ( KnownNat m, KnownNat b ) => Integral ( FX m b ) where
    toInteger t = toInteger $ fromEnum t
    t@( FX a ) `quotRem` ( FX b )
        = let
            (a', b') = a `quotRem` b
            sf = scalingFactor t
        in ( FX $ truncate (fromIntegral a' * sf :: Double), FX b' )

instance Bits ( FX m b ) where
    (.&.) = undefined
    -- ( FX a ) .&. ( FX b ) = FX ( a .&. b )
    (.|.) = undefined
    -- ( FX a ) .|. ( FX b ) = FX ( a .|. b )
    xor = undefined
    -- ( FX a ) `xor` ( FX b ) = FX ( a `xor` b )
    complement = undefined
    -- complement ( FX a ) = FX $ complement a
    shift ( FX a ) i = FX $ shift a i
    rotate ( FX a ) i = FX $ rotate a i

    bitSize = undefined
    -- bitSize ( FX a ) = fromMaybe undefined $ bitSizeMaybe a
    bitSizeMaybe = undefined
    -- bitSizeMaybe ( FX a ) = bitSizeMaybe a
    isSigned = undefined
    -- isSigned ( FX a ) = isSigned a
    testBit = undefined
    -- testBit ( FX a ) = testBit a
    bit = undefined
    -- bit i = FX $ bit i
    popCount = undefined
    -- popCount ( FX a ) = popCount a

instance ( KnownNat b ) => FiniteBits ( FX m b ) where
    finiteBitSize _ = fromInteger $ natVal (Proxy :: Proxy b)

instance ( KnownNat m, KnownNat b ) => Val ( FX m b ) where
    showTypeOf _ = let
            m = natVal (Proxy :: Proxy m)
            b = natVal (Proxy :: Proxy b)
        in "FX" ++ show m ++ "_" ++ show b
    fromVerilog = FX . read
    toVerilogLit (FX x) = fromIntegral x
    verilogHelper x = [qc|
task trace;
    input integer cycle;
    input integer tick;
    input [{ finiteBitSize x }-1:0] actual;
    begin
        $write("%0d:%0d\t", cycle, tick);
        $write("actual: %.3f\t", fxtor(actual));
        $display();
    end
endtask // trace

task assert;
    input integer cycle;
    input integer tick;
    input [{ finiteBitSize x }-1:0] actual;
    input [{ finiteBitSize x }-1:0] expect;
    input [256*8-1:0] var; // string
    begin
        $write("%0d:%0d\t", cycle, tick);
        $write("actual: %.3f\t", fxtor(actual));
        $write("expect: %.3f\t", fxtor(expect));
        $write("var: %0s\t", var);
        if ( !( actual === expect ) ) $write("FAIL");
        $display();
    end
endtask // assert

function real fxtor(input integer x);
    begin
        fxtor = $itor(x) / $itor(1 << { scalingFactorPower x });
    end
endfunction // fxtor
|]

instance ( KnownNat m, KnownNat b ) => FixedPointCompatible ( FX m b ) where
    fractionalBitSize x = finiteBitSize x - fromInteger (natVal (Proxy :: Proxy m))
    scalingFactorPower _
        = let
            m = natVal (Proxy :: Proxy m)
            b = natVal (Proxy :: Proxy b)
        in b - m

instance (KnownNat m, KnownNat b) => Fractional (FX m b) where
    fromRational = fromRational . toRational
    (/) = undefined
    recip = undefined
